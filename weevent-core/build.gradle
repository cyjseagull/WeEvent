apply plugin: "io.spring.dependency-management"
apply plugin: "signing"
def spring_version = "4.3.27.RELEASE"
List spring = [
        "org.springframework:spring-core:$spring_version",
        "org.springframework:spring-beans:$spring_version",
        "org.springframework:spring-context:$spring_version",
        "org.springframework:spring-tx:$spring_version",
]
dependencies {
    compile spring
    // web3sdk for FISCO-BCOS 2.0
    implementation("org.fisco-bcos:java-sdk:${javaSDKVersion.fisco}")
    /*dependencies { compile fileTree(dir:'../lib',include:['*.jar'])}
    compile files("../lib/java-sdk-1.0.0-SNAPSHOT.jar")*/
    // fabric 1.4.4
    implementation("org.hyperledger.fabric-sdk-java:fabric-sdk-java:${fabricVersion}")

    // depend project
    api project(":weevent-client")

    // log for test
    testImplementation("org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}")
}

jar {
    destinationDir file("dist/apps")
    archiveName project.name + "-" + version + ".jar"

    exclude "**/*.xml"
    exclude "**/*.properties"

    doLast {
        copy {
            from file("src/main/resources/")
            into "dist/conf"
        }
        copy {
            from configurations.runtime
            into "dist/lib"
        }
        copy {
            from file("build/libs/")
            into "dist/apps"
        }
    }
}

// for upload to maven
task sourcesJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}
